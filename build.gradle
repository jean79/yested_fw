buildscript {
    repositories {
        jcenter()
    }
}

plugins {
    id 'org.jetbrains.kotlin.multiplatform' version '1.3.40'
    id 'maven-publish'
}
repositories {
    mavenLocal()
    jcenter()
    maven { url "https://dl.bintray.com/kotlin/ktor" }
    mavenCentral()
}

group 'net.yested'
version '1.0-SNAPSHOT'

kotlin {
    js {
        browser {
        }
    }
    sourceSets {
        commonMain {
            dependencies {
                implementation kotlin('stdlib-common')
                implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core-common:1.1.0'
            }
        }
        commonTest {
            dependencies {
                implementation kotlin('test-common')
                implementation kotlin('test-annotations-common')
                implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.1.0'
            }
        }
        jsMain {
            dependencies {
                implementation kotlin('stdlib-js')
                implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core-js:1.1.0'
                api project(':global-jquery')
                implementation npm('bootstrap', '3.3')
                implementation npm('jquery', '3.3.1')
                implementation npm('moment', '2.23.0')
                implementation npm('pickadate', '3.6.2')
            }
        }
        jsTest {
            dependencies {
                implementation kotlin('test-js')
            }
        }
    }
}

publishing {
    publications {
        js(MavenPublication) {
            pom {
                name = 'Yested'
                description = 'Yested is a Kotlin framework for building single-page web applications in Javascript.'
                url = 'http://www.yested.net/'
                licenses {
                    license {
                        name = 'The MIT License (MIT)'
                        url = 'https://github.com/jean79/yested2/blob/master/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = 'jean79'
                        name = 'Jan Kovar'
                        email = 'jan.kovar79@gmail.com'
                    }
                    developer {
                        id = 'epabst'
                        name = 'Eric Pabst'
                        email = 'epabst@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git@github.com:jean79/yested_fw.git'
                    developerConnection = 'scm:git:git@github.com:jean79/yested_fw.git'
                    url = 'https://github.com/jean79/yested_fw'
                }
            }
        }
    }
    repositories {
        maven {
            name = "ossrh"
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
        }
    }
}
